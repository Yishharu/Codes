DECLARE SUB DERIVS (X!, Y!(), DYDX!())
DECLARE SUB RK4 (Y!(), DYDX!(), N!, X!, H!, YOUT!(), DUM!)

SUB RKQC (Y(), DYDX(), N, X, HTRY, EPS, YSCAL(), HDID, HNEXT, DUM)
FCOR = .0666666667#
ONE = 1!
SAFETY = .9
ERRCON = .0006
DIM YTEMP(N), YSAV(N), DYSAV(N)
PGROW = -.2
PSHRNK = -.25
XSAV = X
FOR I = 1 TO N
  YSAV(I) = Y(I)
  DYSAV(I) = DYDX(I)
NEXT I
H = HTRY
DO
  HH = .5 * H
  CALL RK4(YSAV(), DYSAV(), N, XSAV, HH, YTEMP(), DUM)
  X = XSAV + HH
  CALL DERIVS(X, YTEMP(), DYDX())
  CALL RK4(YTEMP(), DYDX(), N, X, HH, Y(), DUM)
  X = XSAV + H
  IF X = XSAV THEN PRINT "Stepsize not significant in RKQC.": EXIT SUB
  CALL RK4(YSAV(), DYSAV(), N, XSAV, H, YTEMP(), DUM)
  ERRMAX = 0!
  FOR I = 1 TO N
    YTEMP(I) = Y(I) - YTEMP(I)
    IF ABS(YTEMP(I) / YSCAL(I)) > ERRMAX THEN ERRMAX = ABS(YTEMP(I) / YSCAL(I))
  NEXT I
  ERRMAX = ERRMAX / EPS
  IF ERRMAX > ONE THEN
    H = SAFETY * H * (ERRMAX ^ PSHRNK)
    FLAG = 1
  ELSE
    HDID = H
    IF ERRMAX > ERRCON THEN
      HNEXT = SAFETY * H * (ERRMAX ^ PGROW)
    ELSE
      HNEXT = 4! * H
    END IF
    FLAG = 0
  END IF
LOOP WHILE FLAG = 1
FOR I = 1 TO N
  Y(I) = Y(I) + YTEMP(I) * FCOR
NEXT I
ERASE DYSAV, YSAV, YTEMP
END SUB

