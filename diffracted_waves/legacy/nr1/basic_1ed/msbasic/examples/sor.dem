DECLARE SUB SOR (A#(), B#(), C#(), D#(), E#(), F#(), U#(), JMAX!, RJAC#)

'PROGRAM D17R1
'Driver for routine SOR
DEFDBL A-H, O-Z
CLS
JMAX = 11
PI = 3.1415926#
DIM A(JMAX, JMAX), B(JMAX, JMAX), C(JMAX, JMAX), D(JMAX, JMAX)
DIM E(JMAX, JMAX), F(JMAX, JMAX), U(JMAX, JMAX)
FOR I = 1 TO JMAX
  FOR J = 1 TO JMAX
    A(I, J) = 1!
    B(I, J) = 1!
    C(I, J) = 1!
    D(I, J) = 1!
    E(I, J) = -4!
    F(I, J) = 0!
    U(I, J) = 0!
  NEXT J
NEXT I
MIDL = INT(JMAX / 2) + 1
F(MIDL, MIDL) = 2!
RJAC = COS(PI / JMAX)
CALL SOR(A(), B(), C(), D(), E(), F(), U(), JMAX, RJAC)
PRINT "SOR Solution:"
FOR I = 1 TO JMAX
  FOR J = 1 TO JMAX
    PRINT USING "###.##"; U(I, J);
  NEXT J
  PRINT
NEXT I
PRINT
PRINT "Test that solution satisfies Difference Eqns:"
FOR I = 2 TO JMAX - 1
  FOR J = 2 TO JMAX - 1
    DUM = 4! * U(I, J)
    F(I, J) = U(I + 1, J) + U(I - 1, J) + U(I, J + 1) + U(I, J - 1) - DUM
  NEXT J
  PRINT "      ";
  FOR J = 2 TO JMAX - 1
    PRINT USING "###.##"; F(I, J);
  NEXT J
  PRINT
NEXT I
END

