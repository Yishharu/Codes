DECLARE SUB MRQMIN (X!(), Y!(), SIG!(), NDATA!, A!(), MA!, LISTA!(), MFIT!, COVAR!(), ALPHA!(), NCA!, CHISQ!, DUM!, ALAMDA!)
DECLARE FUNCTION GASDEV! (IDUM&)
COMMON BETA()

'PROGRAM D14R8A
'Driver for routine MRQMIN
CLS
NPT = 100
MA = 6
SPREAD = .001
DIM BETA(20, 1), X(NPT), Y(NPT), SIG(NPT)
DIM A(MA), LISTA(MA), COVAR(MA, MA), ALPHA(MA, MA), GUES(MA)
FOR I = 1 TO MA
  READ A(I)
NEXT I
DATA 5.0,2.0,3.0,2.0,5.0,3.0
FOR I = 1 TO MA
  READ GUES(I)
NEXT I
DATA 4.5,2.2,2.8,2.5,4.9,2.8
IDUM& = -911
'First try a sum of two Gaussians
FOR I = 1 TO 100
  X(I) = .1 * I
  Y(I) = 0!
  FOR J = 1 TO 4 STEP 3
    Y(I) = Y(I) + A(J) * EXP(-((X(I) - A(J + 1)) / A(J + 2)) ^ 2)
  NEXT J
  Y(I) = Y(I) * (1! + SPREAD * GASDEV(IDUM&))
  SIG(I) = SPREAD * Y(I)
NEXT I
MFIT = MA
FOR I = 1 TO MFIT
  LISTA(I) = I
NEXT I
ALAMDA = -1
FOR I = 1 TO MA
  A(I) = GUES(I)
NEXT I
CALL MRQMIN(X(), Y(), SIG(), NPT, A(), MA, LISTA(), MFIT, COVAR(), ALPHA(), MA, CHISQ, DUM, ALAMDA)
K = 1
ITST = 0
DO
  PRINT
  PRINT "Iteration #"; K; "  Chi-squared:";
  PRINT USING "#####.####"; CHISQ;
  PRINT "   ALAMDA:   ";
  PRINT USING ".##^^^^"; ALAMDA
  PRINT "   A(1)    A(2)    A(3)    A(4)    A(5)    A(6)"
  FOR I = 1 TO 6
    PRINT USING "###.####"; A(I);
  NEXT I
  PRINT
  K = K + 1
  OCHISQ = CHISQ
  CALL MRQMIN(X(), Y(), SIG(), NPT, A(), MA, LISTA(), MFIT, COVAR(), ALPHA(), MA, CHISQ, DUM, ALAMDA)
  IF CHISQ > OCHISQ THEN
    ITST = 0
  ELSEIF ABS(OCHISQ - CHISQ) < .1 THEN
    ITST = ITST + 1
  END IF
LOOP WHILE ITST < 2
ALAMDA = 0!
CALL MRQMIN(X(), Y(), SIG(), NPT, A(), MA, LISTA(), MFIT, COVAR(), ALPHA(), MA, CHISQ, DUM, ALAMDA)
PRINT "Uncertainties:"
FOR I = 1 TO 6
  PRINT USING "###.####"; SQR(COVAR(I, I));
NEXT I
PRINT
PRINT
PRINT "Expected results:"
PRINT USING "####.###"; 5!; 2!; 3!; 2!; 5!; 3!
END

