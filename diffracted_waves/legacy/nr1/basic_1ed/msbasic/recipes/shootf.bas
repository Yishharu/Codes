DECLARE SUB LOAD1 (X1!, V1!(), Y!())
DECLARE SUB LOAD2 (X2!, V2!(), Y!())
DECLARE SUB SCORE (XF!, Y!(), F!())
DECLARE SUB ODEINT (YSTART!(), NVAR!, X1!, X2!, EPS!, H1!, HMIN!, NOK!, NBAD!, DUM1!, DUM2!)
DECLARE SUB LUDCMP (A!(), N!, NVAR!, INDX!(), D!)
DECLARE SUB LUBKSB (A!(), N!, NVAR!, INDX!(), B!())

SUB SHOOTF (NVAR, V1(), V2(), DELV1(), DELV2(), N1, N2, X1, X2, XF, EPS, H1, HMIN, F(), DV1(), DV2())
DIM Y(NVAR), F1(NVAR), F2(NVAR), DFDV(NVAR, NVAR), INDX(NVAR)
CALL LOAD1(X1, V1(), Y())
CALL ODEINT(Y(), NVAR, X1, XF, EPS, H1, HMIN, NOK, NBAD, DERIVS, RKQC)
CALL SCORE(XF, Y(), F1())
CALL LOAD2(X2, V2(), Y())
CALL ODEINT(Y(), NVAR, X2, XF, EPS, H1, HMIN, NOK, NBAD, DERIVS, RKQC)
CALL SCORE(XF, Y(), F2())
J = 0
FOR IV = 1 TO N2
  J = J + 1
  SAV = V1(IV)
  V1(IV) = V1(IV) + DELV1(IV)
  CALL LOAD1(X1, V1(), Y())
  CALL ODEINT(Y(), NVAR, X1, XF, EPS, H1, HMIN, NOK, NBAD, DERIVS, RKQC)
  CALL SCORE(XF, Y(), F())
  FOR I = 1 TO NVAR
    DFDV(I, J) = (F(I) - F1(I)) / DELV1(IV)
  NEXT I
  V1(IV) = SAV
NEXT IV
FOR IV = 1 TO N1
  J = J + 1
  SAV = V2(IV)
  V2(IV) = V2(IV) + DELV2(IV)
  CALL LOAD2(X2, V2(), Y())
  CALL ODEINT(Y(), NVAR, X2, XF, EPS, H1, HMIN, NOK, NBAD, DERIVS, RKQC)
  CALL SCORE(XF, Y(), F())
  FOR I = 1 TO NVAR
    DFDV(I, J) = (F2(I) - F(I)) / DELV2(IV)
  NEXT I
  V2(IV) = SAV
NEXT IV
FOR I = 1 TO NVAR
  F(I) = F1(I) - F2(I)
  F1(I) = -F(I)
NEXT I
CALL LUDCMP(DFDV(), NVAR, NVAR, INDX(), DET)
CALL LUBKSB(DFDV(), NVAR, NVAR, INDX(), F1())
J = 0
FOR IV = 1 TO N2
  J = J + 1
  V1(IV) = V1(IV) + F1(J)
  DV1(IV) = F1(J)
NEXT IV
FOR IV = 1 TO N1
  J = J + 1
  V2(IV) = V2(IV) + F1(J)
  DV2(IV) = F1(J)
NEXT IV
ERASE INDX, DFDV, F2, F1, Y
END SUB

