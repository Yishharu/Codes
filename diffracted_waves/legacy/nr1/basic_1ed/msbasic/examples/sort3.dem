DECLARE SUB SORT3 (N!, RA!(), RB!(), RC!(), WKSP!(), IWKSP!())

'PROGRAM D8R6
'Driver for routine SORT3
CLS
NLEN = 64
DIM A(NLEN), B(NLEN), C(NLEN), WKSP(NLEN), INDX(NLEN)
DIM AMSG$(NLEN), BMSG$(NLEN), CMSG$(NLEN)
MSG1$ = "I'd rather have a bottle in front"
MSG2$ = " of me than a frontal lobotomy."
PRINT "Original message:"
FOR J = 1 TO 64
  AMSG$(J) = MID$(MSG1$ + MSG2$, J, 1)
  PRINT AMSG$(J);
NEXT J
PRINT
'Read array of random numbers
OPEN "TARRAY.DAT" FOR INPUT AS #1
FOR I = 1 TO INT(NLEN / 10)
  LINE INPUT #1, DUM$
  FOR J = 1 TO 10
    A(10 * (I - 1) + J) = VAL(MID$(DUM$, 6 * J - 5, 6))
  NEXT J
NEXT I
LINE INPUT #1, DUM$
FOR J = 1 TO NLEN - 10 * INT(NLEN / 10)
  A(10 * INT(NLEN / 10) + J) = VAL(MID$(DUM$, 6 * J - 5, 6))
NEXT J
CLOSE #1
'Create array B and array C
FOR I = 1 TO NLEN
  B(I) = I
  C(I) = NLEN + 1 - I
NEXT I
'Sort array A while mixing B and C
CALL SORT3(NLEN, A(), B(), C(), WKSP(), INDX())
'Scramble message according to array B
FOR I = 1 TO NLEN
  J = B(I)
  BMSG$(I) = AMSG$(J)
NEXT I
PRINT
PRINT "Scrambled message:"
FOR J = 1 TO 64
  PRINT BMSG$(J);
NEXT J
PRINT
'Unscramble according to array C
FOR I = 1 TO NLEN
  J = C(I)
  CMSG$(J) = BMSG$(I)
NEXT I
PRINT
PRINT "Mirrored message:"
FOR J = 1 TO 64
  PRINT CMSG$(J);
NEXT J
PRINT
END

