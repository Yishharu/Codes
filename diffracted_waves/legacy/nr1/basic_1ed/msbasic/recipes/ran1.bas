DECLARE FUNCTION RAN1! (IDUM&)

FUNCTION RAN1 (IDUM&) STATIC
DIM R(97)
M1& = 259200
IA1& = 7141
IC1& = 54773
RM1 = .0000038580247#
M2& = 134456
IA2& = 8121
IC2& = 28411
RM2 = .0000074373773#
M3& = 243000
IA3& = 4561
IC3& = 51349
IF IDUM& < 0 OR IFF = 0 THEN
  IFF = 1
  IX1& = (IC1& - IDUM&) MOD M1&
  IX1& = (IA1& * IX1& + IC1&) MOD M1&
  IX2& = IX1& MOD M2&
  IX1& = (IA1& * IX1& + IC1&) MOD M1&
  IX3& = IX1& MOD M3&
  FOR J = 1 TO 97
    IX1& = (IA1& * IX1& + IC1&) MOD M1&
    IX2& = (IA2& * IX2& + IC2&) MOD M2&
    R(J) = (CSNG(IX1&) + CSNG(IX2&) * RM2) * RM1
  NEXT J
  IDUM& = 1
END IF
IX1& = (IA1& * IX1& + IC1&) MOD M1&
IX2& = (IA2& * IX2& + IC2&) MOD M2&
IX3& = (IA3& * IX3& + IC3&) MOD M3&
J = 1 + INT((97 * IX3&) / M3&)
IF J > 97 OR J < 1 THEN PRINT "Abnormal exit": EXIT FUNCTION
RAN1 = R(J)
R(J) = (CSNG(IX1&) + CSNG(IX2&) * RM2) * RM1
END FUNCTION

