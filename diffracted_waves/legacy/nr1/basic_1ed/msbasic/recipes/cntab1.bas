DECLARE FUNCTION GAMMQ! (A!, X!)

SUB CNTAB1 (NN(), NI, NJ, CHISQ, DF, PROB, CRAMRV, CCC)
TINY = 1E-30
DIM SUMI(NI), SUMJ(NJ)
SUM = 0
NNI = NI
NNJ = NJ
FOR I = 1 TO NI
  SUMI(I) = 0!
  FOR J = 1 TO NJ
    SUMI(I) = SUMI(I) + NN(I, J)
    SUM = SUM + NN(I, J)
  NEXT J
  IF SUMI(I) = 0! THEN NNI = NNI - 1
NEXT I
FOR J = 1 TO NJ
  SUMJ(J) = 0!
  FOR I = 1 TO NI
    SUMJ(J) = SUMJ(J) + NN(I, J)
  NEXT I
  IF SUMJ(J) = 0! THEN NNJ = NNJ - 1
NEXT J
DF = INT(NNI * NNJ - NNI - NNJ + 1)
CHISQ = 0!
FOR I = 1 TO NI
  FOR J = 1 TO NJ
    EXPCTD = SUMJ(J) * SUMI(I) / SUM
    CHISQ = CHISQ + (NN(I, J) - EXPCTD) ^ 2 / (EXPCTD + TINY)
  NEXT J
NEXT I
PROB = GAMMQ(.5 * DF, .5 * CHISQ)
NDUM = NNI - 1
IF NNJ - 1 < NDUM THEN NDUM = NNJ - 1
CRAMRV = SQR(CHISQ / (SUM * NDUM))
CCC = SQR(CHISQ / (CHISQ + SUM))
ERASE SUMJ, SUMI
END SUB

