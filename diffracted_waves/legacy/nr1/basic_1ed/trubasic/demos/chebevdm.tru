PROGRAM D5r5

! Driver for routine chebev

LIBRARY "chebft", "chebev"
DECLARE FUNCTION func, chebev

LET nval = 40
DIM c(0)
MAT redim c(nval)

CLEAR
LET pio2 = pi/2
LET a = -pio2
LET b = pio2

CALL chebft (a, b, c(), nval, dum)

! Test Chebyshev evaluation routine

DO
   PRINT "How many terms in Chebyshev evaluation?"
   PRINT "Enter n between 6 and "; str$(nval); ". Enter n=0 to end."
   LINE INPUT mval$
   LET mval = val(mval$)
   IF mval <= 0 or mval > nval then EXIT DO

   PRINT "        x        Actual   Chebyshev fit"
   FOR i = -8 to 8 step 1
       LET x = i * pio2 / 10
       PRINT using "----#.######": x, func(x), chebev(a, b, c(), dum, mval, x)
   NEXT i
LOOP

END

FUNCTION func (x)
    LET func = (x ^ 2) * (x ^ 2 - 2) * sin(x)
END FUNCTION
