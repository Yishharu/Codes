DECLARE SUB POLINT (XA!(), YA!(), N!, X!, Y!, DY!)

'PROGRAM D3R1
'Driver for routine POLINT
CLS
NP = 10
PI = 3.1415926#
DIM XA(NP), YA(NP)
PRINT "Generation of interpolation tables"
PRINT " ... sin(x)    0<x<pi"
PRINT " ... exp(x)    0<x<1 "
PRINT "How many entries go in these tables? (note: N<10)"
INPUT N
FOR NFUNC = 1 TO 2
  IF NFUNC = 1 THEN
    PRINT "sine function from 0 to pi"
    FOR I = 1 TO N
      XA(I) = I * PI / N
      YA(I) = SIN(XA(I))
    NEXT I
  ELSEIF NFUNC = 2 THEN
    PRINT "exponential function from 0 to 1"
    FOR I = 1 TO N
      XA(I) = I * 1! / N
      YA(I) = EXP(XA(I))
    NEXT I
  ELSE
    STOP
  END IF
  PRINT "        x         f(x)    interpolated      error"
  FOR I = 1 TO 10
    IF NFUNC = 1 THEN
      X = (-.05 + I / 10!) * PI
      F = SIN(X)
    ELSEIF NFUNC = 2 THEN
      X = -.05 + I / 10!
      F = EXP(X)
    END IF
    CALL POLINT(XA(), YA(), N, X, Y, DY)
    PRINT USING "#####.######"; X; F; Y;
    PRINT "     ";
    PRINT USING "#.####^^^^"; DY
  NEXT I
  PRINT "***********************************"
  PRINT "Press RETURN"
  LINE INPUT DUM$
NEXT NFUNC
END

