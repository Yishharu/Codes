DECLARE SUB TPTEST (DATA1!(), DATA2!(), N!, T!, PROB!)
DECLARE SUB AVEVAR (DATQ!(), N!, AVE!, VAR!)
DECLARE FUNCTION GASDEV! (IDUM&)

'PROGRAM D13R6B
'Driver for routine TPTEST
'Compare two correlated distributions vs. two
'uncorrelated distributions
CLS
NPTS = 500
EPS = .01
NSHFT = 10
ANOISE = .3
DIM DATA1(NPTS), DATA2(NPTS), DATA3(NPTS)
IDUM& = -5
PRINT "              Correlated:                  Uncorrelated:"
PRINT "  Shift       T      Probability           T      Probability"
OFFSET = (NSHFT / 2) * EPS
FOR J = 1 TO NPTS
  GAUSS = GASDEV(IDUM&)
  DATA1(J) = GAUSS
  DATA2(J) = GAUSS + ANOISE * GASDEV(IDUM&)
  DATA3(J) = GASDEV(IDUM&) + ANOISE * GASDEV(IDUM&)
NEXT J
CALL AVEVAR(DATA1(), NPTS, AVE1, VAR1)
CALL AVEVAR(DATA2(), NPTS, AVE2, VAR2)
CALL AVEVAR(DATA3(), NPTS, AVE3, VAR3)
FOR J = 1 TO NPTS
  DATA1(J) = DATA1(J) - AVE1 + OFFSET
  DATA2(J) = DATA2(J) - AVE2
  DATA3(J) = DATA3(J) - AVE3
NEXT J
FOR I = 1 TO NSHFT + 1
  SHIFT = I * EPS
  FOR J = 1 TO NPTS
    DATA2(J) = DATA2(J) + EPS
    DATA3(J) = DATA3(J) + EPS
  NEXT J
  CALL TPTEST(DATA1(), DATA2(), NPTS, T1, PROB1)
  CALL TPTEST(DATA1(), DATA3(), NPTS, T2, PROB2)
  PRINT USING "###.##"; SHIFT;
  PRINT USING "#######.####"; T1; PROB1;
  PRINT "     ";
  PRINT USING "#######.####"; T2; PROB2
NEXT I
END

